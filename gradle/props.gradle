buildscript {
	repositories {
		gradlePluginPortal()
	}
	dependencies {
		classpath 'org.ajoberstar.grgit:grgit-gradle:4.0.+'
	}
}

apply plugin: org.ajoberstar.grgit.gradle.GrgitPlugin


// Generates a file containing the current Git revision
task generateGitRevFile() {

	ext { outputFile = file("$buildDir/tmp/generateGitRev/gitrev") }

	outputs.file outputFile
	outputs.upToDateWhen { false }

	doLast {
		if (!outputFile.parentFile.isDirectory()) { outputFile.parentFile.mkdirs() }
		outputFile.text = grgit.head().id
	}
}

// Define a tasks that creates the version.properties file and stores it in the resources
task generateVersionProperties {

	// define version properties file location
	ext { versionPropFile = file("$buildDir/tmp/generateVersionProperties/version.properties") }

	// configure version properties generation task input and output
	inputs.property "version", version
	inputs.file generateGitRevFile
	outputs.files versionPropFile

	// check if file exists and create it if necessary
	doLast {
		if(!versionPropFile.isFile()){
			versionPropFile.parentFile.mkdirs()
			versionPropFile.createNewFile()
		}
		Properties props = new Properties()
		props.version = version
		props.revision = inputs.files[0].text
		props.store(versionPropFile.newWriter(), null)
	}
}

jar { into('com/rapidminer/tools/') { from generateVersionProperties } }